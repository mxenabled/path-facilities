plugins {
  id "maven-publish"
  id "signing"
}

javaPlatform {
  allowDependencies()
}

// Dependency versions aren't resolved, by default. Check this out to resolve this issue:
// https://github.com/gradle/gradle/issues/8887
dependencies {
  api platform("com.mx.path-core:platform")

  constraints {
    api(project(":message-broker-nats"))
    api(project(":store-vault"))
    api(project(":store-redis"))
    api(project(":fault-tolerant-executor-resilience4j"))
    api(project(":exception-reporter-honeybadger"))
    api(project(":encryption-service-vault"))
    api(project(":encryption-service-jasypt"))
  }
}

publishing {
  publications {
    bom(MavenPublication) {
      from components.javaPlatform
      pom {
        groupId = project.group
        artifactId = project.name
        name = project.name
        description = project.description
        url = "https://github.com/mxenabled/path-core"

        developers {
          developer {
            name = "MX"
            email = "path@mx.com"
            organization = "MX Technologies Inc."
            url = "http://www.mx.com"
          }
        }

        licenses {
          license {
            name = "Proprietary"
            url = "https://github.com/mxenabled/path-core/blob/master/LICENSE"
            distribution = "repo"
          }
        }

        scm {
          connection = "scm:git:git@github.com:mxenabled/path-core.git"
          url = "https://github.com/mxenabled/path-core/tree/master"
        }
      }
    }
  }
}

signing {
  def signingKey = findProperty("signingKey")
  def signingPassword = findProperty("signingKeyPassword")
  if (signingKey != null && signingKey != "") {
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.bom
    logger.lifecycle("Configuring signing for ${project.name}")
  } else {
    logger.lifecycle("Skipping artifact signing for ${project.name} - missing signing key")
  }
}
